plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'io.github.0ffz.github-packages' version '1.2.0'
}

allprojects {
    apply plugin: "kotlin"
    apply plugin: "maven-publish"

    group 'com.mineinabyss'
    //Add run number to version
    if (System.getenv("GITHUB_RUN_NUMBER") != null) version += ".${System.getenv("GITHUB_RUN_NUMBER")}"


    java {
        sourceCompatibility JavaVersion.VERSION_1_8
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
        }
    }
    compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }

    publishing {
        repositories {
            maven githubPackagePublish.invoke("MineInAbyss/Idofront")
        }
        publications {
            gpr(MavenPublication) {
                from components.java
            }
        }
    }
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$server_version" //Spigot
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1" // JVM dependency
    compileOnly "com.charleskorn.kaml:kaml:0.25.0"

    testCompile 'junit:junit:4.12'
}
