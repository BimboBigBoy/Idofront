plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

allprojects {
    apply plugin: "kotlin"
    apply plugin: "maven-publish"

    group 'com.mineinabyss'
    if (project.hasProperty("buildNo")) version += ".$buildNo"

    java {
        setSourceCompatibility(1.8)
        setTargetCompatibility(1.8)
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
        }
    }
    compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/MineInAbyss/Idofront")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.15-R0.1-SNAPSHOT" //Spigot
    compileOnly "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0" // JVM dependency
    compileOnly "com.charleskorn.kaml:kaml:0.17.0"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}